C -------------------------------------------------------------
C ---- HighPressure software
C -------------------------------------------------------------
	SUBROUTINE ABSORB
C
C SUBROUTINE TO CALCULATE ABSORPTION CORRECTION FOR INDIVIDUAL
C REFLECTIONS. test chhange
C
	REAL DENOMP(40),DENOMD(40),XTEST(3)
	INCLUDE 'reflection.inc'
	INCLUDE 'griddata.inc'
	INCLUDE 'crystalmodel.inc'
	INCLUDE 'dac.inc'
	INCLUDE 'Flags.inc'
	REAL MFACTOR,INTFACTOR
C
C INITIALISATION FOR EACH REFLECTION
C  ...need to  be zero as they are the base for summations
      TRNS=0.0
	DTRNS=0.0
	TOTALTRNS=0.0
	GTRNS=0.0
	MEDTRNS=0.
	TOTALINT=0.
C
C CHECK FOR FULL/PARTIAL ILLUMINATION OF CRYSTAL - IF SHADOWING REQUESTED
C
	JSHAD=1			! SET THIS FLAG FOR ALL CRYSTALS AS DEFAULT
	IF(ISHAD .EQ. 1)CALL CHECK_SHADOW(JSHAD)
C
C GET COMMON DENOMINATORS FOR ABSORPTION CORRECTIONS FOR THIS BEAM
C (EQNS 9 AND 10 OF BURNHAM)
C
	IF(IABSCO .EQ. 1)THEN
		DO I=1,I1
	  	  DENOMP(I)=0.
		  DENOMD(I)=0.
		  DO J=1,3
		    DENOMP(I)=DENOMP(I)+B(I,J)*COSP(J)
		    DENOMD(I)=DENOMD(I)+B(I,J)*COSD(J)
		  ENDDO
		ENDDO
	ENDIF
	
C
C START LOOP OVER GRID POINTS
C
	DO 900 I=1,NPT(1)
	  XTEST(1)=XPT(I)
	  DO 899 J=1,NPT(2)
	    XTEST(2)=YPT(I,J)
	    DO 898 K=1,NPT(3)
		XTEST(3)=ZPT(I,J,K)
C
C DO CRYSTAL ABSORPTION FOR THIS POINT
C LOOP OVER CRYSTAL FACES TO FIND SHORTEST PATH FOR EACH BEAM
C

	        PATHPMIN=10000.
	        PATHDMIN=10000.
	        DO JA=1,I1
		  TOP=NUMER(I,J,K,JA)
C
C PRIMARY BEAM - FIRST TRAP CASE OF BEAM PARALLEL TO FACE
C (IN WHICH CASE PATH = INFINITE)
C
		  IF(ABS(DENOMP(JA)) .GT. 1.0E-6)THEN
		    PATHP=TOP/DENOMP(JA)
		    IF(PATHP .GT. 1.0E-5)THEN
		      IF(PATHP .LT. PATHPMIN)THEN
		        PATHPMIN=PATHP
		      ENDIF
		    ENDIF
		  ENDIF
C
C DIFFRACTED BEAM - AGAIN TRAP CASE OF BEAM PARALLEL TO FACE
C
		  IF(ABS(DENOMD(JA)) .GT. 1.0E-6)THEN
		    PATHD=TOP/DENOMD(JA)
		    IF(PATHD .GT. 1.0E-5)THEN
		      IF(PATHD .LT. PATHDMIN)THEN
		        PATHDMIN=PATHD
		      ENDIF
		    ENDIF
		  ENDIF
	        ENDDO
C
C GOT CRYSTAL PATH FOR THIS POINT - DO SUMS CRYSTAL ONLY
C
	        PATH=PATHDMIN+PATHPMIN
	        ABSFACT=XW(I)*YW(I,J)*ZW(I,J,K)*EXP(-1.*ABSCO*PATH)
	        DTRNS=DTRNS-PATH*ABSFACT
                TRNS=TRNS+ABSFACT
C 
C IF CRYSTAL PARTLY SHADOWED - TEST FOR THIS POINT SHADOWED
C
	    IF(JSHAD .NE. 1)THEN
C
C CALCULATE GASKET ABSORPTION FACTOR FOR  BEAM TO THIS POINT
C
		  GFACTOR=GASKETABS(XTEST)
			IF(IFATAL .EQ. 1)RETURN
 		  GTRNS=GTRNS+XW(I)*YW(I,J)*ZW(I,J,K)*GFACTOR
		ELSE
		  GIPATH=0.
		  GDPATH=0.
		  GFACTOR=1.		!NO GASKET ABSORPTION
		ENDIF
C
C AND FOR ABSORBING PRESSURE MEDIA, THE PATH LENGTH IN THE MEDIUM
C
		IF(IMEDIA .EQ. 1)THEN
			MFACTOR=ABS_MEDIA(XTEST,PATH)		!RETURNS EXP(-MU_MEDIA*t)
		    MEDTRNS=MEDTRNS+XW(I)*YW(I,J)*ZW(I,J,K)*MFACTOR
		ELSE
			MFACTOR=1.
		ENDIF
C
C INCIDENT BEAM FUNCTION ADDED 24 FEB 2012
C		
		INTFACTOR=GET_INTENSITY(XTEST)
		IF(IFATAL .EQ. 1)RETURN
		TOTALINT=TOTALINT+XW(I)*YW(I,J)*ZW(I,J,K)*INTFACTOR		!NOT EXPORT - ONLY FOR DEBUGGING
		

C
C FINAL CALCULATION FOR THIS BEAM TO THIS POINT 
C
		TOTALTRNS=TOTALTRNS+ABSFACT*GFACTOR*MFACTOR*INTFACTOR
C
898	    CONTINUE
899	  CONTINUE
900	CONTINUE
C
C FINISH INTEGRALS
C
	TOTALTRNS=TOTALTRNS/XVOL		! FOR ALL CASES
C
	IF(IABSCO .EQ. 1)THEN
	   TRNS=TRNS/XVOL				! CRYSTAL IF ABSORBING
	   DTRNS=DTRNS/XVOL
	ELSE
	   TRNS=1.0						!NON-ABSORBING CRYSTAL
	   DTRNS=1.0
	ENDIF
C
	IF(IDAC .NE. 0)THEN
		IF(JSHAD .EQ. 1)THEN
			GTRNS=1.0			! NO GASKET CORRECTIONS TO BE MADE
		ELSE
			GTRNS=GTRNS/XVOL
		ENDIF
C
		IF(IMEDIA .EQ. 1)THEN
			MEDTRNS=MEDTRNS/XVOL
		ELSE
			MEDTRNS=1.0
		ENDIF
	ELSE
		GTRNS=1.0			! ADDED NO DAC DEFAULT VALUES APRIL 18TH 2012
		MEDTRNS=1.0
	ENDIF
C
	TOTALINT=TOTALINT/XVOL
	RETURN
	END
C
C

C
C
	SUBROUTINE ABSORB_FILLG
C
C SUBROUTINE TO CALCULATE ABSORPTION CORRECTION FOR INDIVIDUAL
C REFLECTIONS FOR CRYSTAL FILLING GASKET MODEL
C
	REAL DENOMP(40),DENOMD(40),XTEST(3)
	INCLUDE 'Flags.inc'
	INCLUDE 'reflection.inc'
	INCLUDE 'griddata.inc'
	INCLUDE 'crystalmodel.inc'
C COMMON DACPATH:		PARI IS TOTAL PATHLENGTH OF I-BEAM INSIDE CELL
C					PARD IS TOTAL PATHLENGTH OF D-BEAM INSIDE CELL
C					GIPATH IS PATHLENGTH OF I-BEAM IN GASKET
C					GIPATH IS PATHLENGTH OF D-BEAM IN GASKET
C
C
C INITIALISATION FOR EACH REFLECTION
C
      TRNS=0.0
	DTRNS=0.0
	TOTALTRNS=0.0
	GTRNS=0.0
	MEDTRNS=1.0			! NO MEDIA SO MDTRANS IS ALWAYS UNITY
	TOTALINT=0.
C
C START LOOP OVER GRID POINTS
C
	DO 900 I=1,NPT(1)
	  XTEST(1)=XPT(I)
	  DO 899 J=1,NPT(2)
	    XTEST(2)=YPT(I,J)
	    DO 898 K=1,NPT(3)
		XTEST(3)=ZPT(I,J,K)
C
C IDENTIFY WHICH BEAMS ARE SHADOWED TO THIS POINT
C
		JSHAD=ISHADOW(XTEST)	! ISHADOW ALSO CALCS TOTAL PATH
		STORED_PATH=PARI+PARD
		IF(JSHAD .EQ. -4)THEN
			GFACTOR=0.			! A BEAM PARALLEL TO ANVIL
		ELSE
C
C GET GASKET FACTOR TO THIS POINT
			GFACTOR=GASKETABS(XTEST) !ALSO CALCS GASKET PATHS
			IF(IFATAL .EQ. 1)RETURN
C
C GET PATH IN CRYSTAL IF ABSORBING
			IF(IABSCO .EQ. 0)THEN
				ABSFACT=XW(I)*YW(I,J)*ZW(I,J,K)
			ELSE
		        PATH=STORED_PATH-GIPATH-GDPATH
				ABSFACT=XW(I)*YW(I,J)*ZW(I,J,K)*EXP(-1.*ABSCO*PATH)
				DTRNS=DTRNS-PATH*ABSFACT
				TRNS=TRNS+ABSFACT
			ENDIF
C
C INCIDENT BEAM FUNCTION ADDED 24 FEB 2012
C		
			INTFACTOR=GET_INTENSITY(XTEST)
			IF(IFATAL .EQ. 1)RETURN
			TOTALINT=TOTALINT+XW(I)*YW(I,J)*ZW(I,J,K)*INTFACTOR		!NOT EXPORT - ONLY FOR DEBUGGING
		

C ADD TOTAL ABSORPTION FACTOR FOR  BEAM TO THIS POINT TO SUMS
C
			GTRNS=GTRNS+XW(I)*YW(I,J)*ZW(I,J,K)*GFACTOR
			TOTALTRNS=TOTALTRNS+ABSFACT*GFACTOR*INTFACTOR
		ENDIF
C
898	    CONTINUE
899	  CONTINUE
900	CONTINUE
C
C FINISH INTEGRALS
C
	IF(IABSCO .EQ. 1)THEN
	   TRNS=TRNS/XVOL
	   DTRNS=DTRNS/XVOL
	ELSE
	   TRNS=1.0
	   DTRNS=1.0
	ENDIF
	IF(JSHAD .NE. 1)THEN
	   GTRNS=GTRNS/XVOL
	   TOTALTRNS=TOTALTRNS/XVOL
	ELSE
	   GTRNS=1.0
	   TOTALTRNS=TRNS
	ENDIF
	TOTALINT=TOTALINT/XVOL
	RETURN
	END
C
C


