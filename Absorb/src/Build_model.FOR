C ***********************************************************************
C
	SUBROUTINE BUILD_MODEL
 	IMPLICIT NONE
	INCLUDE 'Crystalmodel.inc'
	INCLUDE 'Reflection.inc'		! only for dummy cosp, cosd
	INCLUDE 'DAC.inc'				! only for tg
	INCLUDE 'Files.inc'
	INCLUDE 'Flags.inc'				! to indicate which progs run etc
	CHARACTER ETEXT*256
	CHARACTER*80 TEXT
	INTEGER IERR,I
	REAL TOL
C
	IBUILD=0
	IF(IMODELFORM .EQ. 4)THEN
		CALL SETGRID_SPHERE	!SPHERICAL CRYSTAL
		IF(IFATAL .EQ. 1)RETURN
	ELSE IF(IMODELFORM .EQ. 5)THEN
		CALL SETGRID_FILLG		! CRYSTAL FILLED GASKET HOLE
		IF(IFATAL .EQ. 1)RETURN
	ELSE IF(IMODELFORM .NE. 0)THEN
		CALL GETFACES
		IF(IFATAL .EQ. 1)RETURN
		CALL SETGRID
		IF(IFATAL .EQ. 1)RETURN
	ENDIF
C
C OUTPUT THE INFO ABOUT THE MODEL 
C NOTE: IF ERROR IN SETUP THEN WRITE_CRYSTAL CALLED BEFORE PROGRAM HALT
C
	CALL WRITE_CRYSTAL_DATA(IPRT)


C
C DO FURTHER CHECKS FOR CRYSTAL COMPATIBLE WITH DAC MODEL
C
C APART FROM CORNERS BEING IN THE I-BEAM ANVIL, WE NEED THE GASKET INFO
C TO CHECK THE D-BEAM ANVIL AND THE CORNERS BEING IN THE GASKET
C
	IF(ISHAD .EQ. 1 .AND. IMODELFORM .NE. 5)THEN
C CHECK ALL OF CRYSTAL IS INSIDE GASKET HOLE 
C
C first CHECK FOR DAC THAT ALL Y-COORDS LIE BETWEEN 0 AND TGASKET
C  - DO BY TESTING THE CORNER COORDS
C
		TOL=5.		! 5 MICRON TOLERANCE TO ALLOW CONTINUE (1 MICRON NOT REPORTED)
		IERR=1
		DO I=1,NCORN
			IF(CORNER(I,2) .LT. -1.0)THEN
				WRITE(ETEXT,290)I
290				FORMAT('ERROR IN CRYSTAL MODEL, CORNER ',I3,
	1	' HAS NEGATIVE Y-COORDINATE, LIES IN I-BEAM ANVIL!')
				IF(CORNER(I,2) .LT. -1.0*TOL)THEN				
					CALL ERROR(ETEXT)
					RETURN
				ELSE
					CALL WARNING(ETEXT)
				ENDIF
			ENDIF
			IF(CORNER(I,2) .GT. TG+1.0)THEN
				WRITE(ETEXT,291)I
291				FORMAT('ERROR IN CRYSTAL MODEL, CORNER ',I3,
	1	' HAS Y-COORDINATE > GASKET T, LIES IN D-BEAM ANVIL!')
				IF(CORNER(I,2) .GT. TG+TOL)THEN
					CALL ERROR(ETEXT)
					RETURN
				ELSE
					CALL WARNING(ETEXT)
				ENDIF
			ENDIF
		ENDDO
c now USE ISHADOW WITH
C BOTH BEAMS SET PARALLEL TO CELL AXIS (IE PARALELL TO BLEVY Y AXIS)
C
	  DO I=1,3
		COSP(I)=0.
		COSD(I)=0.
	  ENDDO
	  COSP(2)=1.
	  COSD(2)=1.
	  CALL CHECK_SHADOW(IERR)
	  IF(IERR .NE. 1)THEN
		WRITE(ETEXT,164)
164		FORMAT('CRYSTAL MODEL DOES NOT FIT INTO GASKET HOLE')
		CALL ERROR(ETEXT)
		RETURN
	  ENDIF
	IERR=0
	ENDIF

C
C	
	IBUILD=1

C
	RETURN
	END

