C -------------------------------------------------------------
C
C
	SUBROUTINE WRITE_CIF_ABSORB
C
C SUBROUTINE TO OPEN A CIF FILE AND APPEND THE ABSORB INFO
C
	include 'Crystalmodel.inc'
	include 'DAC.inc'
	INCLUDE 'totals.inc'
	include 'files.inc'
	include 'facestore.inc'
	include 'unitcell.inc'
c
	character cifname*256,stars*20
	character*256 text
c
	stars='********************'
C
C  OPEN FILE OUTPUT_NAME+absorb.CIF', A
C
	icif=30
	i=index(fname(4),'.')-1
	cifname=fname(4)(1:i)//'_absorb.cif'
	open(unit=icif,file=cifname,STATUS='replace',
     1	CARRIAGECONTROL='FORTRAN',ERR=999)

C
C WRITE OUT PROGRAM INFO
C
	write(icif,10)stars,stars,stars,version_no,stars,stars,stars
10	format(' #',3a20/' #'/
	1' #  Information concerning absorption corrections '/
     1' #              generated by Absorb v',f5.2/
	1' #'/' #',3a20/)
c
	write(icif,"(' _computing_data_reduction'/
	1' ; Absorb7 and Absorb-GUI, Angel and Gonzalez-Platas (2013)'/
     2'   J. Appl. Cryst. 46:252-254'/
     2' ;'/)")
C
C DEAL WITH NO ABSORPTION CASE
	IF(IDAC .EQ. 0 .AND. IABSCO .EQ. 0)THEN
		write(icif,20)
20		format(' _exptl_absorpt_correction_T_max      1.0'/
	1	' _exptl_absorpt_correction_T_min      1.0'/
	2	' _exptl_absorpt_correction_type        none')
		RETURN
	ENDIF

C
C WRITE ABSCOEFF in mm-1 AND MIN AND MAX: this is the total transmission
C
	write(icif,30)ABSCO*1000.0,totaltrns_max,totaltrns_min
30	format(' _exptl_absorpt_coefficient_mu      ',f6.3/
	1       ' _exptl_absorpt_correction_T_max   ',f6.3/
	1	   ' _exptl_absorpt_correction_T_min   ',f6.3/
     1	   ' _exptl_absorpt_correction_type    Gaussian'//)

C
C start of the difficult stuff
c
c  first the method and grid points
c
	write(icif,40)
40	format(' _exptl_absorpt_process_details'/' ;')
c
c sphere or otherwise
	if (imodelform .eq. 4)then
		ipt=NINT(NPT(1)**3.)
		write(icif,60)npt(1),npt(1),npt(1),ipt
	else
	 	IPT=NPT(1)*NPT(2)*NPT(3)
		write(icif,60)npt,ipt
60		format(' Gaussian integration over a grid'/' of ' ,
     1  I2,' x ',I2,' x ',I2,' points = ',i5,' total grid points'/
     2'   Based upon method of Burnham (1966)'/)
	endif
C
C WRITE OUT SPECIALS (EG DAC)
	IF(IDAC .ne. 0)then
		WRITE(Icif,150)DACABSMIN,DACABSMAX
150		FORMAT('  Data corrected for diamond-anvil cell absorption'/
	1	'  Note that exptl_absorpt_correction_tmin and _tmax'/
	1	'  the total correction factors applied to the intensities'/
     2	'  The individual factors are: '/
     3   8x,'  range of dac transmission factors (min-max) ',2F10.3)
c	
		IF(ISHAD .EQ. 1)THEN
			WRITE(icif,727)gtrans_min,gtrans_max
727			FORMAT(8X,'  range of gasket transmission (min-max)      '
	1					,2F10.3)
			WRITE(icif,728)medtrans_min,medtrans_max
728			FORMAT(8X,'  range of P media transmission (min-max)     '
	1					,2F10.3)
		ENDIF
	    IF(IDAC .LE. 4)THEN
			DO I=1,2	
c
c thickness code and mu of anvils and plattens changed 15 August 2012 to match new use of mm
c
			WRITE(Icif,151)I,TDIAMOND(I),MU_DIA(I)
151			FORMAT(/'  thickness of diamond anvil ',I1,': ',F6.3,
	1				' mm, mu = ',F7.4,' mm-1')
			WRITE(Icif,156)I,TBERYL(I),MU_BE(I)
156			FORMAT('        thickness of platten ',I1,': ',F6.3,
	1			' mm, mu = ',F7.4,' mm-1')
			ENDDO
		ELSE
			IF(NTERMS_PSI .LT. 0)THEN
				WRITE(Icif,157)ABSPSI_PAR(1),ABSPSI_PAR(2)
157				FORMAT(/' DAC transmission function',
	1		' with sum(mu*t) for anvil 1:',F7.3,'  anvil 2:',F7.3)
			ELSE
				WRITE(Icif,158)
158				FORMAT(/'     DAC transmission function',
	1			' via a table of values')
			ENDIF
		ENDIF
		IF(ISHAD .EQ. 1)THEN
			WRITE(Icif,170)TG,RGASKET
170			FORMAT(/5x,'Gasket shadowing corrections were made ',
     1		'based upon'/,5x,'Gasket thickness = ',F5.1,
     2	     ' microns, radius = ',F5.1,' microns')

			IF(IOPAQUE .EQ. 0)THEN
				 WRITE(icif,172)GASKETMU*1000.
172				 FORMAT(5X,'Gasket absorption coeff = ',F12.2,' mm-1')
			ELSE
				WRITE(icif,174)
174				FORMAT(5X,'Gasket assumed totally opaque')
			ENDIF			
			WRITE(icif,173)THRESHO
173			FORMAT(5X,'REFLECTION WAS CONSIDERED TOTALLY OBSCURED IF '
     2		/'  FRACTION CRYSTAL ILLUMINATED WAS LESS THAN ',F4.2/)
C
C
				IF(IMEDIA .EQ. 1)THEN
					WRITE(icif,181)MU_MEDIA*1000.
181				FORMAT(5X,'Absorption coeff of pressure medium = ',
	1					F6.2,' mm-1')
				ELSE
					WRITE(icif,182)
182					FORMAT(5X,'Non-absorbing pressure medium')
				ENDIF
		ELSE
165		  WRITE(icif,175)
175		  FORMAT(/5X,'No gasket shadowing corrections were made'//)
		ENDIF
	ENDIF
c
c terminate block for process_details
	write(icif,"(' ;')")
c
c   write the exptl_crystal part
c
c first the dac (again)
	if(idac .ne. 0)then
		write(icif,"(' _exptl_crystal_preparation',5x,
	1' ''mounted in a diamond-anvil cell '' ')")
	endif
c
c branch on imodelform = 1 hkl,d
c                      = 4 sphere
c                      = 5 filled gasket
c                      otherwise
c  

	if(imodelform .eq. 4)then
		write(icif,400)xtal_rad/1000.
400		format(' _exptl_crystal_description           sphere'/
	1	         ' _exptl_crystal_size_rad             ',f8.6)
c
c
	else if(imodelform .eq. 1)then
		write(icif,"(' _exptl_crystal_description      ?')")
		write(icif,410)    			
410		format(' loop_'/' _exptl_crystal_face_index_h'/
	1	' _exptl_crystal_face_index_k'/' _exptl_crystal_face_index_l'/
	2	' _exptl_crystal_face_perp_dist')
c
c changed 3 may to using hkld because this covers the case when dac crystal card used
c		do i=1,i1
c			read(face_store(i),*)ih,ik,il,d
c			write(icif,420)ih,ik,il,d/1000.
c420			format(3i6,f10.6)
c		enddo
		do i=1,i1
			write(icif,470)(hkld(i,j),j=1,4)
		enddo


		write(icif,"(' ')")
c	
	else if(imodelform .eq. 5)then
		write(icif,430)
430		format(' _exptl_crystal_description ',5x,
	1	' ''crystal fills hole of diamond-cell gasket'' '/)
c
c
	else if(iub .eq. 0)then
		write(icif,440)
440		format(' _exptl_crystal_description '/' ;'/
	1'  crystal was described in terms of coordinates of corners'/
	2'  on the orthogonal phi-axis coordinate system of ',
     2' Busing and Levy (1967)'/
	3'  (ie +Y along beam, +Z up at circles zero,',
     4' +X to make right-handed set)')
		if(idac .ne. 0)then
			write(icif,"('  with origin at the centre of the face',
	2		' of the incident-beam anvil')")
		endif
		write(icif,"('   loop is over x, y, z (mm) ')")
		do i=1,ncorn
			write(icif,450)(corner(i,j)/1000.,j=1,3)
450			format(3f12.6)
		enddo
		write(icif,"(' ;')")
	else
		write(icif,460)
460		format(' _exptl_crystal_description '/' ;'/
	1'  crystal was described in terms of coordinates of corners'/
	2'  now back-transformed to potentially non-integer hkl'/' ;')
		write(icif,410)    			! writes header for hkld loop
		do i=1,i1
			write(icif,470)(hkld(i,j),j=1,4)
470			format(5x,4f10.4)
		enddo
		write(icif,"(' ')")
c
	endif




	close(unit=icif)
	RETURN
c
c error opening file
c
999	write(text,"('ERROR OPENING CIF FILE - NOT WRITTEN')")
	call WARNING(TEXT)
	RETURN
	END